#
# A theme based on steeef Prompt.
#
# Authors:
#   steeef <steeef@gmail.com>
#

function prompt_awesome_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="(${_prompt_awesome_colors[1]}%b%f%u%c${_prompt_awesome_colors[4]}●%f)"
  else
    branch_format="(${_prompt_awesome_colors[1]}%b%f%u%c)"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_awesome_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_awesome_precmd

  # You could use extended color pallete if available...
  # if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
  #   _prompt_awesome_colors=(
  #     "%F{81}"  # Turquoise
  #     "%F{166}" # Orange
  #     "%F{red}" # Purple
  #     "%F{161}" # Hotpink
  #     "%F{118}" # Limegreen
  #     "%F{118}" # Limegreen
  #   )
  # else
  #   _prompt_awesome_colors=(
  #     "%F{blue}"
  #     "%F{yellow}"
  #     "%F{red}"
  #     "%F{red}"
  #     "%F{green}"
  #     "%F{cyan}"
  #   )
  # fi
  # ... but for now I'm happy with standard colors
  _prompt_awesome_colors=(
    "%F{blue}"
    "%F{yellow}"
    "%F{red}"
    "%F{red}"
    "%F{green}"
    "%F{cyan}"
  )
  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_awesome_colors[1]}%b%f%u%c)"
  local action_format="(${_prompt_awesome_colors[5]}%a%f)"
  local unstaged_format="${_prompt_awesome_colors[2]}●%f"
  local staged_format="${_prompt_awesome_colors[5]}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

  # Define prompts.
  # Formatting explained here: http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
  PROMPT="
${_prompt_awesome_colors[3]}%n%f at ${_prompt_awesome_colors[2]}%m%f in ${_prompt_awesome_colors[5]}%~%f "'${vcs_info_msg_0_}'"
"'${_prompt_awesome_colors[6]}$python_info[virtualenv]%f'"$ "
  RPROMPT=''
}

prompt_awesome_setup "$@"
